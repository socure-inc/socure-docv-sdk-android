apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    useLibrary 'org.apache.http.legacy'
    compileSdkVersion 32

    defaultConfig {
        applicationId "com.socure.demo.demosk"
        minSdkVersion 22
        targetSdkVersion 32
        versionCode 2021
        versionName "2.2.2"
    }

    def localProperties = new Properties()
    localProperties.load(new FileInputStream(rootProject.file("local.properties")))

    signingConfigs {
        release {
            storeFile file(localProperties['keystore.release.filepath'])
            storePassword localProperties['keystore.release.password']
            keyAlias localProperties['key.release.alias']
            keyPassword localProperties['key.release.password']
        }
    }

    buildTypes {
        debug {
            debuggable true
        }

        release {
            debuggable false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11
    }

    viewBinding {
        enabled = true
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "${variant.name}-${variant.versionName}.apk"
        }
    }
}

allprojects {
    repositories {
        maven { url 'https://jitpack.io'}
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'com.karumi:dexter:6.2.1'
    implementation 'com.google.code.gson:gson:2.9.0'

    // socure document verification sdk dependency
    implementation 'com.github.socure-inc:android-sdk:2.2.2'
}


